pkgname=antimicro-git
pkgver=2.23.r14.gecec251
pkgrel=1

pkgdesc='map keyboard and mouse actions to gamepad buttons, inspired by qjoypad'
url='https://github.com/AntiMicro/antimicro'
arch=('i686' 'x86_64')
license=('GPL3')

depends=('libxtst' 'qt5-base' 'sdl2' 'libxkbcommon-x11')
makedepends=('cmake' 'qt5-tools')

provides=('antimicro')
conflicts=('antimicro')

source=('git+https://github.com/AntiMicro/antimicro')

sha1sums=('SKIP')

pkgver() {
    cd antimicro
    git describe --long --tags | sed 's/^v//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd antimicro
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DUSE_SDL_2=ON
    make VERBOSE=1
}

package() {
    cd antimicro
    make DESTDIR="$pkgdir" install
}
