pkgname=wayland-git
pkgver=1.15.0.r28.gfde6046
pkgrel=1

pkgdesc='display server protocol'
url='http://wayland.freedesktop.org'
arch=('i686' 'x86_64')
license=('MIT')

depends=('libffi' 'expat' 'libxml2')

provides=('wayland')
conflicts=('wayland')

source=('git+https://anongit.freedesktop.org/git/wayland/wayland')

sha1sums=('SKIP')

pkgver() {
    cd wayland
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd wayland
    autoreconf -fi
    # XXX documentation is failing to build
    ./configure --prefix=/usr --disable-static --disable-documentation
    make
}

package() {
    cd wayland
    make DESTDIR="$pkgdir" install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
