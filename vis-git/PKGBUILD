pkgname=vis-git
pkgver=0.7.r24.g0cccd6e
pkgrel=1

pkgdesc='vi-like text editor'
url='http://www.brain-dump.org/projects/vis/'
arch=('x86_64')
license=('ISC')

depends=('ncurses' 'libtermkey' 'lua' 'tre' 'lua-lpeg')
makedepends=('git')
checkdepends=('vim')

provides=('vis')
conflicts=('vis')

source=('git+https://github.com/martanne/vis.git'
        'git+https://github.com/martanne/vis-test.git')

sha256sums=('SKIP' 'SKIP')

prepare() {
    cd vis
    git submodule init
    git config submodule.test.url "$srcdir"/vis-test
    git submodule update test
}

pkgver() {
    cd vis
    git describe --long --tags | sed 's/^v//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd vis
    ./configure --prefix=/usr
    make
}

check() {
    cd vis
    # The digraph tests need a UTF-8 locale.
    LC_ALL=en_US.UTF-8 make -C test
}

package() {
    cd vis
    make DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/vis/LICENSE
}
