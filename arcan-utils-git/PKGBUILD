pkgname=arcan-utils-git
pkgver=0.6.0.pre2.r88.g3a5260f4
pkgrel=1

pkgdesc='experimental tools for arcan'
url='https://arcan-fe.com/'
arch=('x86_64')
license=('GPL' 'BSD')

depends=('arcan')
makedepends=('git' 'cmake' 'fuse3' 'hidapi')
optdepends=('fuse3: to mount the window configuration filesystem with arcan_cfgfs')

provides=('arcan-utils')
conflicts=('arcan-utils')

source=('git+https://github.com/letoram/arcan')

sha256sums=('SKIP')

pkgver() {
    cd arcan
    git describe --long --tags | sed -r 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
    cd arcan
    # Attempt to include all of the available tools not formally built with the
    # main cmake.

    # netproxy clones https://github.com/letoram/UDT during configure
    # vrbridge probably needs openhmd, builds anyway
    # kbdconv needs a copy of kbd source (keymap/common.h)
    CFLAGS+=" -fmacro-prefix-map=$PWD/="
    for tool in acfgfs aclip aloadimage shmmon vrbridge; do
        env -C "$srcdir"/arcan/src/tools/"$tool" cmake -DCMAKE_INSTALL_PREFIX=/usr .
        env -C "$srcdir"/arcan/src/tools/"$tool" make VERBOSE=1
    done
}

package() {
    cd arcan

    for tool in acfgfs aclip aloadimage shmmon vrbridge; do
        make -C src/tools/"$tool" DESTDIR="$pkgdir" install
    done
}
