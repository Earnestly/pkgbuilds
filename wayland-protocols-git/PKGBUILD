pkgname=wayland-protocols-git
pkgver=1.11.r20.g0130366
pkgrel=1

pkgdesc='additional protocols not available in wayland core'
url='http://wayland.freedesktop.org'
arch=('any')
license=('MIT')

provides=('wayland-protocols')
conflicts=('wayland-protocols')

source=('git+https://anongit.freedesktop.org/git/wayland/wayland-protocols')

sha1sums=('SKIP')

pkgver() {
    cd wayland-protocols
    git describe --long --tags | sed 's/^v//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd wayland-protocols
    autoreconf -fi
    ./configure --prefix=/usr
}

check() {
    cd wayland-protocols
    make test-suite.log
}

package() {
    cd wayland-protocols
    make DESTDIR="$pkgdir" install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
