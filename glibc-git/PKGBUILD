# * Maybe generate en_US.UTF-8 until glibc adds C.UTF-8?

#     https://sourceware.org/glibc/wiki/Proposals/C.UTF-8
#     https://src.fedoraproject.org/cgit/rpms/glibc.git/tree/glibc-c-utf8-locale.patch

# * Investigate whether I can build glibc without all the charmaps and just
#   UTF-8, this would cut down testing time.

pkgname=glibc-git
pkgver=2.30.9000
pkgrel=1

pkgdesc='gnu c library'
url='http://www.gnu.org/software/libc'
arch=('x86_64')
groups=('base')
license=('GPL' 'LGPL')

options=('!strip' 'debug' 'staticlibs')
backup=('etc/gai.conf' 'etc/nscd.conf')
install='glibc-git.install'

depends=('linux-api-headers' 'tzdata' 'filesystem')
optdepends=('gd: graph image generation with memusage'
            'sh: needed to run locale-gen script')
makedepends=('git')

# XXX Arch Linux's valgrind package requires an exact version
provides=("glibc=${pkgver%.*.*.*}")
conflicts=('glibc')

source=('git+https://sourceware.org/git/glibc.git'
        'locale-gen'
        'nscd.service'
        'nscd.tmpfiles')

sha256sums=('SKIP'
            'b54556d33f3414b0073701fa331b53a858f130dd0e82fb82ae91bba7247f14e8'
            '0d261c4b677b8a7a820febaccd60877da091a4200fa05dc3e0c2b140254f313a'
            '79c9cec5dd2414dcb76d11e75903aa6fd9b7785c897cb9f853edcb140a71b779')

pkgver() {
    cd glibc
    git describe | sed 's/^glibc-//; s/-/.r/; s/-/./'
}

prepare() {
    cd glibc
    mkdir -p build
}

build() {
    cd glibc/build

    { printf 'slibdir=/usr/lib\n'
      printf 'rtlddir=/usr/lib\n'
      printf 'sbindir=/usr/bin\n'
      printf 'rootsbindir=/usr/bin\n'
    } >> configparms

    CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}

    # This is handled properly by --enable-bind-now
    LDFLAGS=${LDFLAGS/,-z,now/}

    "$srcdir"/glibc/configure \
        --prefix=/usr \
        --libdir=/usr/lib \
        --libexecdir=/usr/lib \
        --with-headers=/usr/include \
        --with-bugurl=https://bugs.archlinux.org/ \
        --enable-cet \
        --enable-add-ons \
        --enable-bind-now \
        --enable-multi-arch \
        --enable-static-pie \
        --enable-lock-elision \
        --enable-stack-protector=strong \
        --enable-stackguard-randomization \
        --disable-werror \
        --disable-profile

    printf 'build-programs=no\n' >> configparms
    make

    # Re-enable hardening for programs.
    sed -i '/build-programs=/s/no/yes/' configparms
    { printf 'CC += -D_FORTIFY_SOURCE=2\n'
      printf 'CXX += -D_FORTIFY_SOURCE=2\n'
    } >> configparms
    make

}

check() {
    cd glibc/build
    # Remove harding in preparation to run test-suite.
    # sed -i '/FORTIFY/d' configparms
    # make check || true
}

package() {
    cd glibc/build
    make install_root="$pkgdir" install

    install -dm0755 "$pkgdir"/usr/lib/locale
    install -dm0755 "$pkgdir"/etc/locales
    install -Dm0755 "$srcdir"/locale-gen "$pkgdir"/usr/bin/locale-gen
    install -Dm0644 "$srcdir"/glibc/posix/gai.conf "$pkgdir"/etc/gai.conf
    install -Dm0644 "$srcdir"/glibc/nscd/nscd.conf "$pkgdir"/etc/nscd.conf
    install -Dm0644 "$srcdir"/nscd.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/nscd.conf
    install -Dm0644 "$srcdir"/nscd.service "$pkgdir"/usr/lib/systemd/system/nscd.service

    # Only support UTF-8 charmaps.
    find "$pkgdir"/usr/share/i18n/charmaps ! -name UTF-8.gz -type f -delete

    # We generate these in the post-install with ldconfig -r .
    rm -f "$pkgdir"/etc/ld.so.cache

    # I use 2> /dev/null for all of these due to many false-positives as it
    # attempts to strip scripts or other unstrippable files.
    find "$pkgdir"/usr/bin -type f -executable -exec strip $STRIP_BINARIES {} + 2> /dev/null || true

    # Do not strip these for gdb and valgrind functionality, but strip the
    # rest.
    find "$pkgdir"/usr/lib ! -name 'ld-*.so' \
                           ! -name 'libc-*.so' \
                           ! -name 'libpthread-*.so' \
                           ! -name 'libthread_db-*.so' \
                             -name '*-*.so' -type f -exec strip $STRIP_SHARED {} + 2> /dev/null || true

    # Strip the remaining static libraries.
    find "$pkgdir"/usr/lib -name '*.a' -type f -exec strip $STRIP_STATIC {} + 2> /dev/null || true

    # Provided by tzdata now.
    rm -f "$pkgdir"/usr/bin/tzselect "$pkgdir"/usr/bin/zdump "$pkgdir"/usr/bin/zic
}
