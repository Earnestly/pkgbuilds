pkgname=opendoas-git
pkgver=6.0.r61.g9a9495d
pkgrel=1

pkgdesc='run commands as another user, alternative to sudo'
url='https://github.com/Duncaen/OpenDoas'
arch=('i686' 'x86_64')
license=('MIT')

install='opendoas.install'

depends=('pam')
makedepends=('git')

provides=('opendoas')
conflicts=('opendoas')

source=('git+https://github.com/Duncaen/OpenDoas')

sha256sums=('SKIP')

pkgver() {
    cd  OpenDoas
    git describe --long --tags | sed 's/^v//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd OpenDoas
    ./configure --prefix=/usr --with-timestamp
    make
}

package() {
    cd  OpenDoas
    make DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
