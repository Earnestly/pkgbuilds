pkgname=pacman-git
pkgver=5.1.3.r180.g39c20ad4
pkgrel=1

pkgdesc='package manager with dependency support'
url='http://www.archlinux.org/pacman/'
arch=('x86_64')
license=('GPL')

groups=('base' 'base-devel')
backup=('etc/pacman.conf' 'etc/makepkg.conf')

depends=('curl' 'gpgme' 'libarchive' 'pacman-mirrorlist' 'archlinux-keyring')
makedepends=('git' 'asciidoc')
checkdepends=('python2' 'fakechroot')

provides=("pacman=$pkgver")
conflicts=('pacman')

source=('git+https://git.archlinux.org/pacman.git'
        'https://git.archlinux.org/svntogit/packages.git/plain/pacman/trunk/pacman.conf'
        'https://git.archlinux.org/svntogit/packages.git/plain/pacman/trunk/makepkg.conf')

sha256sums=('SKIP'
            '95b3b2416402059cf6acf3e046082e7ce261e2b88629231dbf579a4200d8a63b'
            '650ddad24cad6afc4aecb4829cb8f06b9acb70c10a44f756fe8bd279949b518e')

pkgver() {
    cd pacman
    _tag=$(git tag --sort=v:refname | tail -n1)
    printf '%s.r%s.g%s' "${_tag#v}" "$(git rev-list "$_tag"..HEAD --count)" "$(git rev-parse --short HEAD)"
}

build() {
    cd pacman
    autoreconf -fi
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-ldconfig=/usr/bin/ldconfig \
        --with-scriptlet-shell=/usr/bin/bash \
        --enable-doc \
        --enable-debug \
        --enable-git-version
    make
}

check() {
    cd pacman
    make check
}

package() {
    # XXX Get bash completions sorted out
    cd pacman
    make DESTDIR="$pkgdir" install

    install -Dm0644 "$srcdir"/pacman.conf "$pkgdir"/etc/pacman.conf
    install -Dm0644 "$srcdir"/makepkg.conf "$pkgdir"/etc/makepkg.conf
}
