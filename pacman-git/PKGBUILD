pkgname=pacman-git
pkgver=6.0.0alpha1.r76.gede73f4d
pkgrel=1

pkgdesc='package manager with dependency support'
url='http://www.archlinux.org/pacman/'
arch=('x86_64')
license=('GPL')

options=('debug' '!strip')

groups=('base' 'base-devel')
backup=('etc/pacman.conf' 'etc/makepkg.conf')

depends=('curl' 'gpgme' 'libarchive' 'pacman-mirrorlist' 'archlinux-keyring')
makedepends=('git' 'asciidoc')
checkdepends=('python2' 'fakechroot')

provides=("pacman=$pkgver")
conflicts=('pacman')

source=('git+https://git.archlinux.org/pacman.git'
        'https://git.archlinux.org/svntogit/packages.git/plain/pacman/trunk/pacman.conf'
        'https://git.archlinux.org/svntogit/packages.git/plain/pacman/trunk/makepkg.conf')

sha256sums=('SKIP'
            '95b3b2416402059cf6acf3e046082e7ce261e2b88629231dbf579a4200d8a63b'
            '650ddad24cad6afc4aecb4829cb8f06b9acb70c10a44f756fe8bd279949b518e')

prepare() {
    cd pacman
    # Dull version colour numbers in summary
    git revert --no-edit -n b323528491470ff77815fea97289c2cbf7e29689
}

pkgver() {
    cd pacman
    _tag=$(git tag --sort=v:refname | tail -n1)
    printf '%s.r%s.g%s' "${_tag#v}" "$(git rev-list "$_tag"..HEAD --count)" "$(git rev-parse --short HEAD)"
}

build() {
    mkdir -p pacman/build
    cd pacman/build
    meson --prefix=/usr \
          --buildtype=plain \
          -Ddoc=enabled \
          -Duse-git-version=true \
          -Dscriptlet-shell=/usr/bin/bash \
          -Dldconfig=/usr/bin/ldconfig \
          ..
    ninja
}

check() {
    cd pacman/build
    ninja test
}

package() {
    # XXX Get bash completions sorted out
    cd pacman/build
    DESTDIR="$pkgdir" ninja install

    install -Dm0644 "$srcdir"/pacman.conf "$pkgdir"/etc/pacman.conf
    install -Dm0644 "$srcdir"/makepkg.conf "$pkgdir"/etc/makepkg.conf
}
