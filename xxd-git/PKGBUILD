pkgname=xxd-git
pkgver=8.1.0115
pkgrel=1

pkgdesc='create or reverse hex dumps'
url='http://www.vim.org/'
arch=('i686' 'x86_64')
license=('MIT')

depends=('glibc')
makedepends=('git')

conflicts=('xxd' 'vim')
provides=('xxd')

source=('git+https://github.com/vim/vim'
        'LICENSE')

sha256sums=('SKIP'
            '2ac99e6ff2ee077cfec2133d0d5d2d4f14b156994c4c1896703cda98c2d21845')

pkgver() {
    cd vim
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd vim/src/xxd
    make
}

package() {
    cd vim/src/xxd
    install -Dm755 xxd "$pkgdir"/usr/bin/xxd
    install -Dm644 "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
    install -Dm644 "$srcdir"/vim/runtime/doc/xxd.1 "$pkgdir"/usr/share/man/man1/xxd.1
}
