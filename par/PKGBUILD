pkgname=par
pkgver=1.52
pkgrel=2

pkgdesc='paragraph reformatter'
url='http://www.nicemice.net/par/'
arch=('i686' 'x86_64')
license=('custom')

depends=('glibc')

source=("http://www.nicemice.net/par/Par${pkgver/./}.tar.gz"
        "http://sysmic.org/dl/par/par-$pkgver-i18n.4.patch"
        'correct-makefile.patch')

sha256sums=('33dcdae905f4b4267b4dc1f3efb032d79705ca8d2122e17efdecfd8162067082'
            '2ab2d6039529aa3e7aff4920c1630003b8c97c722c8adc6d7762aa34e795861e'
            'a70e7babc18d512aa8daf130e956665140996559c4aea3a2965ca60fe16a9bfd')

prepare() {
    cd Par"${pkgver/./}"
    sed -n '67,85p' par.doc > LICENSE

    # http://sysmic.org/dotclear/index.php?post/2006/06/22/55-add-multibyte-characters-support-in-par
    patch -N -i "$srcdir"/par-"$pkgver"-i18n.4.patch
    patch -Np1 -i "$srcdir"/correct-makefile.patch
}

pkgver() {
    curl -s "$url" | awk '/<title>/ {print $2}'
}

build() {
    cd Par"${pkgver/./}"
    make -f protoMakefile
}

package() {
    cd Par"${pkgver/./}"
    install -Dm0755 par "$pkgdir"/usr/bin/par
    install -Dm0644 par.1 "$pkgdir"/usr/share/man/man1/par.1
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
