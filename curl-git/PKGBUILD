pkgname=curl-git
pkgver=7.60.0.r115.g75e8dc050
pkgrel=1

pkgdesc='url retrieval utility and library'
url='http://curl.haxx.se/'
arch=('i686' 'x86_64')
license=('MIT')

depends=('ca-certificates' 'krb5' 'libssh2' 'openssl' 'zlib' 'libpsl' 'libnghttp2')
makedepends=('git')
# checkdepends=('perl' 'python' 'diff' 'stunnel' 'openssh' 'nghttp2' 'groff')

provides=("curl=$pkgver" 'libcurl.so')
conflicts=('curl')

source=('git+https://github.com/curl/curl.git')

sha256sums=('SKIP')

pkgver() {
    cd curl
    git describe --long --tags | sed -r 's/^curl-//; s/_/./g; s/-/.r/; s/-/./'
}

build() {
    cd curl
    ./buildconf

    # XXX Explicitly disable brotli support instead of including it as a
    #     dependency to keep curl-git consistent with Arch Linux.
    # XXX https://github.com/curl/curl/issues/2202
    PATH_SEPARATOR=: ./configure --prefix=/usr \
        --mandir=/usr/share/man \
        --with-gssapi \
        --with-random=/dev/urandom \
        --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
        --without-brotli \
        --enable-ipv6 \
        --enable-manual \
        --enable-threaded-resolver \
        --enable-versioned-symbols \
        --disable-ldap \
        --disable-ldaps
    make
}

check() {
    cd curl
    # make test
}

package() {
    cd curl
    make DESTDIR="$pkgdir" install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
