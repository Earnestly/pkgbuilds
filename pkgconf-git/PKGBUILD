pkgname=pkgconf-git
pkgver=1.7.3.r17.gdcf529b
pkgrel=1

pkgdesc='compiler and linker configuration'
url='http://pkgconf.org'
arch=('x86_64')
license=('ISC')

depends=('glibc')
makedepends=('popt' 'git' 'meson')
# checkdepends=('kyua' 'atf')

provides=('pkg-config' 'pkgconf')
conflicts=('pkg-config' 'pkgconf')

source=('git+https://github.com/pkgconf/pkgconf')

sha256sums=('SKIP')

pkgver() {
    cd pkgconf
    git describe --long --tags | sed 's/^pkgconf-//; s/-/.r/; s/-/./'
}

build() {
    cd pkgconf
    meson --prefix=/usr --sysconfdir=/etc -Dtests=false build
    ninja -C build
}

check() {
    cd pkgconf
    # Prevent kyua from creating files under ~/.kyua by confining it to the
    # current directory instead.
    # HOME=$PWD ninja -C build test
}

package() {
    cd pkgconf
    DESTDIR="$pkgdir" ninja -C build install

    # Provide this as a drop-in replacement for pkg-config.
    ln -s /usr/bin/pkgconf "$pkgdir"/usr/bin/pkg-config

    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
