pkgname=pkgconf-git
pkgver=1.4.2.r40.gab8df57
pkgrel=1

pkgdesc='compiler and linker configuration'
url='https://git.dereferenced.org/pkgconf/pkgconf'
arch=('i686' 'x86_64')
license=('ISC')

depends=('glibc')
makedepends=('popt' 'git' 'meson')
checkdepends=('kyua' 'atf')

provides=('pkg-config' 'pkgconf')
conflicts=('pkg-config' 'pkgconf')

source=('git+https://git.dereferenced.org/pkgconf/pkgconf')

sha256sums=('SKIP')

pkgver() {
    cd pkgconf
    git describe --long --tags | sed 's/^pkgconf-//; s/-/.r/; s/-/./'
}

build() {
    cd pkgconf
    meson --prefix=/usr --sysconfdir=/etc build
    ninja -C build
}

check() {
    cd pkgconf
    # Prevent kyua from creating files under ~/.kyua by confining it to the
    # current directory instead.
    HOME=$PWD ninja -C build test
}

package() {
    cd pkgconf
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING

    # Provide this as a drop-in replacement for pkg-config.
    ln -s /usr/bin/pkgconf "$pkgdir"/usr/bin/pkg-config
}
