pkgname=acpi_call-mainline-git
pkgver=1.1.0.r0.gac67445
pkgrel=1

pkgdesc='kernel module enabling calls to acpi methods through /proc/acpi/call'
url='https://github.com/mkottman/acpi_call'
arch=('x86_64')
license=('GPL')

depends=('linux-mainline')

source=('git+https://github.com/mkottman/acpi_call'
        'https://github.com/mkottman/acpi_call/pull/75.patch')

sha256sums=('SKIP'
            'a34f80ba83b00bfe1943482d558ed1895e105114110c458b8ac898726bab0bb6')

prepare() {
    cd acpi_call
    # https://github.com/mkottman/acpi_call/pull/49
    # https://github.com/mkottman/acpi_call/pull/52
    # https://github.com/mkottman/acpi_call/pull/65
    # https://github.com/mkottman/acpi_call/pull/74
    # https://github.com/mkottman/acpi_call/pull/75
    patch -Np1 -i "$srcdir"/75.patch

}

pkgver() {
    cd acpi_call
    git describe --long --tags | sed -r 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
    cd acpi_call
    read -r _kversion < /usr/lib/modules/extramodules-mainline/version
    make KVERSION="$_kversion"
}

package() {
    cd acpi_call
    gzip -9 acpi_call.ko
    install -Dm0644 acpi_call.ko.gz "$pkgdir"/usr/lib/modules/extramodules-mainline/acpi_call.ko.gz

    printf 'acpi_call\n' | install -Dm0644 /dev/stdin "$pkgdir"/usr/lib/modules-load.d/acpi_call.conf

    install -dm0755 "$pkgdir"/usr/share/acpi_call
    cp -a --no-preserve=ownership examples support "$pkgdir"/usr/share/acpi_call
}
