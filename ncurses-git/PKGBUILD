pkgname=ncurses-git
pkgver=6.1.20190727.r0.g0dcc4349
pkgrel=1

pkgdesc='terminal control based on srv4 curses'
url='https://invisible-island.net/ncurses/ncurses.html'
arch=('x86_64')
license=('MIT')

depends=('glibc' 'gcc-libs')
makedepends=('git')

# Library provides now needed for Arch Linux's readline package.
provides=('ncurses' 'libtinfo' 'libncursesw.so=6-64')
conflicts=('ncurses' 'libtinfo')

source=('git+https://github.com/ThomasDickey/ncurses-snapshots?signed')

# "Thomas Dickey <dickey@invisible-island.net>"
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB')


sha256sums=('SKIP')

pkgver() {
    cd ncurses-snapshots
    git describe --long --tags | sed 's/^v//; s/_/\./g; s/-/.r/; s/-/./'
}

build() {
    cd ncurses-snapshots
    ./configure --prefix=/usr \
        --with-shared \
        --with-normal \
        --with-cxx-shared \
        --with-cxx-binding \
        --with-manpage-format=normal \
        --with-pkg-config-libdir=/usr/lib/pkgconfig \
        --without-ada \
        --without-debug \
        --enable-widec \
        --enable-pthread \
        --enable-pc-files \
        --enable-hard-tabs \
        --enable-weak-symbols
    make
}

package() {
    cd ncurses-snapshots
    # The target install.libs is needed to include the pkgconfig .pc files.
    make DESTDIR="$pkgdir" install install.libs

    # Ensure that projects linking against non-wide curses libraries use the
    # wide character varients instead.
    for lib in ncurses ncurses++ form panel menu; do
        printf 'INPUT(-l%sw)\n' "$lib" > "$pkgdir"/usr/lib/lib"$lib".so
        ln -sf -- "$lib"w.pc "$pkgdir"/usr/lib/pkgconfig/"$lib".pc
    done

    for lib in tic tinfo; do
        printf 'INPUT(libncursesw.so.%s)\n' "${pkgver:0:1}" > "$pkgdir"/usr/lib/lib"$lib".so
        ln -sf libncursesw.so."${pkgver:0:1}" "$pkgdir"/usr/lib/lib"$lib".so."${pkgver:0:1}"
        ln -sf ncursesw.pc "$pkgdir"/usr/lib/pkgconfig/"$lib".pc
    done

    # Make projects link against ncurses instead when attempting to link with
    # curses.
    printf 'INPUT(-lncursesw)\n' > "$pkgdir"/usr/lib/libcursesw.so
    ln -sf libncurses.so "$pkgdir"/usr/lib/libcurses.so

    # Include some introduction and informal guides as they're quite nicely written.
    install -Dm0644 doc/hackguide.doc "$pkgdir"/usr/share/doc/"$pkgname"/hackguide.doc
    install -Dm0644 doc/ncurses-intro.doc "$pkgdir"/usr/share/doc/"$pkgname"/ncurses-intro.doc

    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
