pkgname=polyml-git
pkgver=5.7.1.r481.g0a6ebca4
pkgrel=1

pkgdesc='standard ml implementation'
url='http://www.polyml.org/'
arch=('i686' 'x86_64')
license=('GPL')

options=('staticlibs' '!libtool')

depends=('libffi' 'gcc-libs')
makedepends=('git')

provides=('polyml')

# XXX mesa-demos has claimed /bin/poly but my mesa-demos-git package adds a
#     'mesa-' prefix to all of the bins which avoids the conflict.  So long
#     as Arch Linux's mesa-demos is not used, this is fine.
conflicts=('polyml')

source=('git+https://github.com/polyml/polyml')

sha256sums=('SKIP')

pkgver() {
    cd polyml
    git describe --long --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd polyml
    ./configure --prefix=/usr --with-system-libffi --with-gmp
    make
    make compiler
}

check() {
    cd polyml
    make tests
}

package() {
    cd polyml
    make DESTDIR="$pkgdir" install
}
