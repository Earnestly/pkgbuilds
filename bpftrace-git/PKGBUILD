pkgname=bpftrace-git
pkgver=0.9.2.r4.gffbf6b2
pkgrel=1

pkgdesc='tracing language for linux ebpf'
url='https://github.com/iovisor/bpftrace'
arch=('x86_64')
license=('Apache')

depends=('bcc' 'clang')
makedepends=('git' 'cmake' 'llvm')

provides=('bpftrace')
conflicts=('bpftrace')

source=('git+https://github.com/iovisor/bpftrace')

sha256sums=('SKIP')

pkgver() {
    cd bpftrace
    git describe --long --tags | sed -r 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
    cd bpftrace
    CFLAGS+=" -fmacro-prefix-map=$PWD/=" cmake -DCMAKE_INSTALL_PREFIX=/usr .
    make VERBOSE=1
}

check() {
    cd bpftrace
    # XXX Needs sudo to run these.
    # make test
}

package() {
    cd bpftrace
    make DESTDIR="$pkgdir" install

    # XXX Incorrect mandir: https://github.com/iovisor/bpftrace/issues/851
    install -dm0755 "$pkgdir"/usr/share
    mv -f -- "$pkgdir"/usr/man "$pkgdir"/usr/share

    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
