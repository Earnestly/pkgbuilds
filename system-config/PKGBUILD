pkgbase=system-config
pkgname=(system-config-base system-config-teapot system-config-engine)
pkgver=16
pkgrel=3

pkgdesc='system configurations'
arch=('any')
url='https://github.com/Earnestly'
license=('custom')

source=('doas-wheel.conf'
        'env-locale.sh'
        'env-nvidia.sh'
        'etc-zshenv'
        'etc-locale.conf'
        'etc-vconsole.conf'
        'etc-pacreport.conf'
        'hook-patch-system-configs.hook'
        'hook-patch-system-configs.scripts'
        'hwdb-caps-as-ctrl-holtek-generic.hwdb'
        'hwdb-caps-as-ctrl-thinkpad.hwdb'
        'locale-en_GB'
        'mod-i915.conf'
        'mod-pcspkr.conf'
        'mod-usbhid.conf'
        'mod-uvcvideo.conf'
        'mod-thinkpad_acpi.conf'
        'nft-nftables.conf'
        'patch-makepkg.conf.patch'
        'patch-pacman.conf.patch'
        'sudo-wheel'
        'sysctl-swap.conf'
        'sysctl-dirty.conf'
        'sysctl-fq_codel.conf'
        'sysctl-overcommit.conf'
        'udev-enable-bfq-scheduler.rules'
        'xorg-trackpoint.conf'
        'xorg-libinput-tweaks.conf'
        'xorg-libinput-noaccel.conf'
        'xorg-nvidia.conf'
        'xorg-xwrapper.config')

package_system-config-base() {
    pkgdesc='common system configurations'
    depends=('zsh' 'sudo')

    # Named locale2.sh due to NoExtract=etc/profile.d/locale.sh
    install -Dm0644 env-locale.sh "$pkgdir"/etc/profile.d/locale2.sh

    install -Dm0644 etc-zshenv "$pkgdir"/etc/zsh/zshenv
    install -Dm0644 etc-locale.conf "$pkgdir"/etc/locale.conf
    install -Dm0644 etc-vconsole.conf "$pkgdir"/etc/vconsole.conf
    install -Dm0600 doas-wheel.conf "$pkgdir"/etc/doas.conf
    install -Dm0644 locale-en_GB "$pkgdir"/etc/locales/en_GB
    install -Dm0644 mod-pcspkr.conf "$pkgdir"/etc/modprobe.d/pcspkr.conf
    install -Dm0644 mod-usbhid.conf "$pkgdir"/etc/modprobe.d/usbhid.conf
    install -Dm0644 etc-pacreport.conf "$pkgdir"/etc/pacreport.conf

    # Make sure sudoers.d is 0700 instead of the default 0755.
    install -dm0700 "$pkgdir"/etc/sudoers.d
    install -m0600 sudo-wheel "$pkgdir"/etc/sudoers.d/wheel

    install -Dm0644 sysctl-dirty.conf "$pkgdir"/etc/sysctl.d/dirty.conf
    install -Dm0644 sysctl-fq_codel.conf "$pkgdir"/etc/sysctl.d/fq_codel.conf
    install -Dm0644 sysctl-overcommit.conf "$pkgdir"/etc/sysctl.d/overcommit.conf
    install -Dm0644 udev-enable-bfq-scheduler.rules "$pkgdir"/etc/udev/rules.d/01-enable-bfq-scheduler.rules
    install -Dm0644 xorg-xwrapper.config "$pkgdir"/etc/X11/Xwrapper.config

    ln -sf /usr/share/zoneinfo/Europe/London "$pkgdir"/etc/localtime
}

package_system-config-teapot() {
    pkgdesc='system configurations for teapot'
    depends=('system-config-base' 'xf86-input-libinput' 'nftables')

    install -Dm0644 hook-patch-system-configs.hook "$pkgdir"/usr/share/libalpm/hooks/patch-system-configs.hook
    install -Dm0755 hook-patch-system-configs.scripts "$pkgdir"/usr/share/libalpm/scripts/patch-system-configs
    install -Dm0644 hwdb-caps-as-ctrl-holtek-generic.hwdb "$pkgdir"/etc/udev/hwdb.d/61-caps-as-ctrl-holtek-generic.hwdb
    install -Dm0644 hwdb-caps-as-ctrl-thinkpad.hwdb "$pkgdir"/etc/udev/hwdb.d/61-caps-as-ctrl-thinkpad.hwdb
    install -Dm0644 mod-i915.conf "$pkgdir"/etc/modprobe.d/i915.conf
    install -Dm0644 mod-thinkpad_acpi.conf "$pkgdir"/etc/modprobe.d/thinkpad_acpi.conf
    install -Dm0644 nft-nftables.conf "$pkgdir"/etc/nftables.conf
    install -Dm0644 patch-pacman.conf.patch "$pkgdir"/usr/share/system-config/patches/pacman.conf.patch
    install -Dm0644 patch-makepkg.conf.patch "$pkgdir"/usr/share/system-config/patches/makepkg.conf.patch
    install -Dm0644 sysctl-swap.conf "$pkgdir"/etc/sysctl.d/swap.conf
    install -Dm0644 xorg-trackpoint.conf "$pkgdir"/etc/X11/xorg.conf.d/13-trackpoint.conf
    install -Dm0644 xorg-libinput-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/11-libinput-tweaks.conf

    printf 'teapot\n' | install -Dm0644 /dev/stdin "$pkgdir"/etc/hostname
}

package_system-config-engine() {
    pkgdesc='system configurations for engine'
    depends=('system-config-base' 'xf86-input-libinput' 'xf86-input-wacom' 'nftables'
             'nvidia' 'nvidia-utils' 'nvidia-settings')

    install -Dm0644 env-nvidia.sh "$pkgdir"/etc/profile.d/nvidia.sh
    install -Dm0644 hwdb-caps-as-ctrl-holtek-generic.hwdb "$pkgdir"/etc/udev/hwdb.d/61-caps-as-ctrl-holtek-generic.hwdb
    install -Dm0644 xorg-nvidia.conf "$pkgdir"/etc/X11/xorg.conf.d/12-nvidia.conf
    install -Dm0644 xorg-libinput-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/11-libinput-tweaks.conf
    install -Dm0644 xorg-libinput-noaccel.conf "$pkgdir"/etc/X11/xorg.conf.d/12-libinput-noaccel.conf

    printf 'engine\n' | install -Dm0644 /dev/stdin "$pkgdir"/etc/hostname
}

sha256sums=('f7dff6ac70e2fb8080082a733be21bad9d433c0677971e74fb245aab31410024'
            '762d71b358f4c5a2033cc90305f569ffdc63052bc0a47669509d975125211810'
            'c88726acdb7b2a7d51f8c3ada4c8dabb23ab75f7c82d64516551f267da37bd21'
            'f6f0c7a9cf710c0c2722c3f42d9eca690c8206bfe4660d33f99e321de52e9cce'
            '337ad308b494e9d3261fc6b75fdd450917a2b14930f675824acf358a2dadc815'
            '2c09b43e51de87a82ff5eeb1c599ce25800b8d34810f14044bf73644e9f7b742'
            '8ac0414ab254c9001e8bb4122ec23b91ed96a6fce8ac29397ecc8318023f7455'
            'e03ee1956c15ab4041dc5a29c978d0017ac5d8bce782f813b55bd0193fa7672e'
            '9e17874e5db1bc22a2fdc5bbb1902069f545f82d926db0ab7df6c962925f3178'
            'f18fb2629138504be8631f365d44f5de37e02e2eaa2d862fcdeed3df9350c571'
            '14a5257880b3d92aa018299d54e97bd356732aa22a24ca9f3db61a7d8dadc4a4'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            'f9925917734d29150d50118651152e032e0415381dfabb5de0947d4062a5c8d9'
            'e30f05181fa754a9b1071ef48bf50f0296d9cbe16a8ba097a95df40506af8ce3'
            'f83b630e1fd67c37090932bd48154ba9f356c4fa519ddeca75b1f97f2bba3f73'
            'f17d620ed2de0d5b568b6eb389f6c55852e615c8b26772e404242cbf3b857f41'
            '04b936fe10b6daa5dbc79c7e642aa9a43b2a9b2de94312bfc3b41ff04a1a9c80'
            'bf9d6ba62e41cc317476c496e273d766209c1ff4fe4b0353ecf6c92f41d11568'
            'e79ee1891c19b816b800dedab8030448c2ca5a4331a542a830cf16eac699b8de'
            '4162dd71a44301c4f7079d9481f9a3c713d00f01a0f35c6cbb71de50869b2dde'
            '1d6e65e7dcf0af1a341a414d5659b5feaf5e5d9b3e53334d531d8ec7e52be5a4'
            '0c164df1a0702723c2e2a48fe7224b2e7318cd83253126c44293ff4c9ff0ba2e'
            'f3ac45e8e1c835886b3daa647033c54e889acc8766c9fbd7bccb80a006438f5d'
            '6a3a3d076b01d99693edba864270ce85e125fed853ac79fe32827c499cdccbe6'
            '7f70b50ef5d9c46196cf0f5e6ff287e4861bb0b8624a5c0914201058d2d281f2'
            'fa72ba0e0252414d9913cd002bbaf74c983cf0da20b648f8592efc91f939a018'
            'fddca78e68e9454a45c0b39bc07ee18bdc9ad34db49ec63bf102816ed18e9c65'
            'def18791c8aba7165603c48072be3b431212ed0dc67cf441833714c6858cfc25'
            'a2e65efdd328d1952692198667140aeb1be6269ba0f238c6d7970783c051348a'
            'f4d33b79eec0256e123d947e7334b90caeed0b1f4ad732ee44bcc12b19b486f7'
            '80a02a54f5a5cec1ecfaaef9d78a10a5d6d47c6bfcfc7d3f95d526e999408e92')
