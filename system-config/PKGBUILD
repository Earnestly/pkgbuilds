pkgbase=system-config
pkgname=(base-config teapot-config engine-config)
pkgver=12.3
pkgrel=1

pkgdesc='system configurations'
arch=('any')
url='https://github.com/Earnestly'
license=('custom')

source=('env-locale.sh'
        'env-nvidia.sh'
        'etc-zshenv'
        'etc-locale.conf'
        'etc-vconsole.conf'
        'hook-patch-system-configs.hook'
        'hook-patch-system-configs.scripts'
        'hwdb-caps-as-ctrl-holtek-generic.hwdb'
        'hwdb-caps-as-ctrl-thinkpad.hwdb'
        'locale-en_GB'
        'mod-i915.conf'
        'mod-pcspkr.conf'
        'mod-usbhid.conf'
        'mod-uvcvideo.conf'
        'mod-thinkpad_acpi.conf'
        'nft-nftables.conf'
        'patch-pacman.conf.patch'
        'sudo-wheel'
        'sysctl-swap.conf'
        'sysctl-dirty.conf'
        'sysctl-fq_codel.conf'
        'udev-enable-bfq-scheduler.rules'
        'xorg-trackpoint.conf'
        'xorg-libinput-tweaks.conf'
        'xorg-libinput-noaccel.conf'
        'xorg-xwrapper.config')


package_base-config() {
    pkgdesc='common system configuration'
    depends=('zsh' 'sudo')

    # Named locale2.sh due to use of NoExtract=etc/profile.d/locale.sh
    install -Dm0644 env-locale.sh "$pkgdir"/etc/profile.d/locale2.sh
    install -Dm0644 etc-zshenv "$pkgdir"/etc/zsh/zshenv
    install -Dm0644 etc-locale.conf "$pkgdir"/etc/locale.conf
    install -Dm0644 etc-vconsole.conf "$pkgdir"/etc/vconsole.conf
    install -Dm0755 hook-patch-system-configs.hook "$pkgdir"/usr/share/libalpm/hooks/patch-system-configs.hook
    install -Dm0755 hook-patch-system-configs.scripts "$pkgdir"/usr/share/libalpm/scripts/patch-system-configs
    install -Dm0644 locale-en_GB "$pkgdir"/etc/locales/en_GB
    install -Dm0644 mod-pcspkr.conf "$pkgdir"/etc/modprobe.d/pcspkr.conf
    install -Dm0644 mod-usbhid.conf "$pkgdir"/etc/modprobe.d/usbhid.conf
    install -Dm0644 patch-pacman.conf.patch "$pkgdir"/usr/share/system-config/patches/pacman.conf.patch
    install -Dm0600 sudo-wheel "$pkgdir"/etc/sudoers.d/wheel
    install -Dm0644 sysctl-dirty.conf "$pkgdir"/etc/sysctl.d/dirty.conf
    install -Dm0644 sysctl-fq_codel.conf "$pkgdir"/etc/sysctl.d/fq_codel.conf
    install -Dm0644 udev-enable-bfq-scheduler.rules "$pkgdir"/etc/udev/rules.d/01-enable-bfq-scheduler.rules
    install -Dm0644 xorg-xwrapper.config "$pkgdir"/etc/X11/Xwrapper.config

    ln -sf /usr/share/zoneinfo/Europe/London "$pkgdir"/etc/localtime
}

package_teapot-config() {
    pkgdesc='system configurations for teapot'
    depends=('base-config' 'xf86-input-libinput' 'nftables')

    install -Dm0644 hwdb-caps-as-ctrl-holtek-generic.hwdb "$pkgdir"/etc/udev/hwdb.d/61-caps-as-ctrl-holtek-generic.hwdb
    install -Dm0644 hwdb-caps-as-ctrl-thinkpad.hwdb "$pkgdir"/etc/udev/hwdb.d/61-caps-as-ctrl-thinkpad.hwdb
    install -Dm0644 mod-i915.conf "$pkgdir"/etc/modprobe.d/i915.conf
    install -Dm0644 mod-thinkpad_acpi.conf "$pkgdir"/etc/modprobe.d/thinkpad_acpi.conf
    install -Dm0644 nft-nftables.conf "$pkgdir"/etc/nftables.conf
    install -Dm0644 sysctl-swap.conf "$pkgdir"/etc/sysctl.d/swap.conf
    install -Dm0644 xorg-trackpoint.conf "$pkgdir"/etc/X11/xorg.conf.d/13-trackpoint.conf
    install -Dm0644 xorg-libinput-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/11-libinput-tweaks.conf

    printf 'teapot\n' | install -Dm0644 -T /dev/stdin "$pkgdir"/etc/hostname
}

package_engine-config() {
    pkgdesc='system configurations for engine'
    depends=('base-config' 'xf86-input-libinput' 'xf86-input-wacom' 'nftables'
             'nvidia' 'nvidia-utils' 'nvidia-settings')

    install -Dm0644 env-nvidia.sh "$pkgdir"/etc/profile.d/nvidia.sh
    install -Dm0644 hwdb-caps-as-ctrl-holtek-generic.hwdb "$pkgdir"/etc/udev/hwdb.d/61-caps-as-ctrl-holtek-generic.hwdb
    install -Dm0644 xorg-libinput-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/11-libinput-tweaks.conf
    install -Dm0644 xorg-libinput-noaccel.conf "$pkgdir"/etc/X11/xorg.conf.d/12-libinput-noaccel.conf

    printf 'engine\n' | install -Dm0644 -T /dev/stdin "$pkgdir"/etc/hostname
}

sha256sums=('762d71b358f4c5a2033cc90305f569ffdc63052bc0a47669509d975125211810'
            'c88726acdb7b2a7d51f8c3ada4c8dabb23ab75f7c82d64516551f267da37bd21'
            'f6f0c7a9cf710c0c2722c3f42d9eca690c8206bfe4660d33f99e321de52e9cce'
            '337ad308b494e9d3261fc6b75fdd450917a2b14930f675824acf358a2dadc815'
            '2c09b43e51de87a82ff5eeb1c599ce25800b8d34810f14044bf73644e9f7b742'
            '9700e8f0f5a202d2a787e1202651c3ad0cd1d7af3dcb891907186900c61ceebd'
            'e562987cf08f3f722e11e6054098933b0f8e714f5ccba6beb1da950b9adbdf7b'
            'f18fb2629138504be8631f365d44f5de37e02e2eaa2d862fcdeed3df9350c571'
            '14a5257880b3d92aa018299d54e97bd356732aa22a24ca9f3db61a7d8dadc4a4'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            'f9925917734d29150d50118651152e032e0415381dfabb5de0947d4062a5c8d9'
            'e30f05181fa754a9b1071ef48bf50f0296d9cbe16a8ba097a95df40506af8ce3'
            'f83b630e1fd67c37090932bd48154ba9f356c4fa519ddeca75b1f97f2bba3f73'
            'f17d620ed2de0d5b568b6eb389f6c55852e615c8b26772e404242cbf3b857f41'
            '04b936fe10b6daa5dbc79c7e642aa9a43b2a9b2de94312bfc3b41ff04a1a9c80'
            'bf9d6ba62e41cc317476c496e273d766209c1ff4fe4b0353ecf6c92f41d11568'
            'eda636a26e88a040448ed8d6f49325a45cd26ca5bd8f5f3e565b82ab02116fe5'
            '1d6e65e7dcf0af1a341a414d5659b5feaf5e5d9b3e53334d531d8ec7e52be5a4'
            '0c164df1a0702723c2e2a48fe7224b2e7318cd83253126c44293ff4c9ff0ba2e'
            'f3ac45e8e1c835886b3daa647033c54e889acc8766c9fbd7bccb80a006438f5d'
            '6a3a3d076b01d99693edba864270ce85e125fed853ac79fe32827c499cdccbe6'
            'fa72ba0e0252414d9913cd002bbaf74c983cf0da20b648f8592efc91f939a018'
            'fddca78e68e9454a45c0b39bc07ee18bdc9ad34db49ec63bf102816ed18e9c65'
            'def18791c8aba7165603c48072be3b431212ed0dc67cf441833714c6858cfc25'
            'a2e65efdd328d1952692198667140aeb1be6269ba0f238c6d7970783c051348a'
            '5c8980aa8e9719d5542b444db80ea610ce8d98a81f5f608d4dc5f0b69ca741e6')
