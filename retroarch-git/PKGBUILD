pkgname=retroarch-git
pkgver=1.5.0.r84.g9e5d6c415
pkgrel=1

pkgdesc='frontend for the libretro api'
url='http://github.com/libretro/RetroArch'
arch=('i686' 'x86_64')
license=('GPL')

depends=('nvidia-cg-toolkit' 'libxv' 'sdl2' 'jack' 'libpulse' 'libusb'
         'libxinerama' 'ffmpeg' 'openal' 'libxkbcommon')
makedepends=('git')
optdepends=('python: transform cg shaders to glsl')

provides=('retroarch')
conflicts=('retroarch')

source=('git+https://github.com/libretro/RetroArch.git')

sha1sums=('SKIP')

pkgver() {
    cd RetroArch
    git describe --long --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd RetroArch
    ./configure --prefix=/usr \
        --enable-cg \
        --enable-kms \
        --enable-udev \
        --enable-sdl2 \
        --enable-pulse \
        --enable-libxml2 \
        --enable-threads \
        --enable-freetype
    make
}

package() {
    cd RetroArch
    make DESTDIR="$pkgdir" install
}
