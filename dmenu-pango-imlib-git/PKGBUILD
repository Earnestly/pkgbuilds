pkgname=dmenu-pango-imlib-git
pkgver=4.5.3.r9.ga9066ec
pkgrel=1

pkgdesc='dynamic menu with pango and imlib support'
url='https://github.com/Cloudef/dmenu-pango-imlib'
arch=('i686' 'x86_64')
license=('MIT')

depends=('libxinerama' 'pango' 'imlib2')

conflicts=('dmenu' 'dmenu-pango-imlib')
provides=('dmenu' 'dmenu-pango-imlib')

source=('git+https://github.com/Cloudef/dmenu-pango-imlib')

sha1sums=('SKIP')

pkgver() {
    cd dmenu-pango-imlib
    git describe --long --tags | sed 's/^v//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd dmenu-pango-imlib
    make 
}

package() {
    cd dmenu-pango-imlib
    make PREFIX=/usr DESTDIR=$pkgdir install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
