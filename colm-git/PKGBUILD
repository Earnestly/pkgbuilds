pkgname=colm-git
pkgver=0.14.7.r0.ge88bda06
pkgrel=1

pkgdesc='programming language designed for the analysis and transformation of computer languages'
url='http://www.colm.net/open-source/colm'
arch=('x86_64')
license=('MIT')

depends=('sh')

# XXX configure.ac only checks for gdc-8 through gdc-5 and not gdc itself.
#     Because of this 'gcc-d' cannot be used for the time being.
makedepends=('git' 'asciidoc' 'fig2dev' 'julia' 'go' 'ocaml' 'mono'
             'java-environment-common' 'ruby')

source=('git+https://github.com/adrian-thurston/colm')

provides=('colm')
conflicts=('colm')

sha256sums=('SKIP')

pkgver() {
    cd colm
    git describe --long --tags | sed 's/^colm-//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd colm
    autoreconf -fi
    ./configure --prefix=/usr
    make
}

check() {
    cd colm
    ./test/runtests
}

package() {
    cd colm
    make DESTDIR="$pkgdir" install
    rm -f -- "$pkgdir"/usr/share/doc/colm/colm.vim
    rm -f -- "$pkgdir"/usr/share/runtests
    install -Dm0644 colm.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/colm.vim
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
