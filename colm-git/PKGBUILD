pkgname=colm-git
pkgver=0.13.0.6.r3.g75ea06cb
pkgrel=1

pkgdesc='programming language designed for the analysis and transformation of computer languages'
url='http://www.colm.net/open-source/colm'
arch=('i686' 'x86_64')
license=('MIT')

depends=('gcc-libs')

source=('git://colm.net/colm.git')

provides=('colm')
conflicts=('colm')

sha256sums=('SKIP')

pkgver() {
    cd colm
    git describe --long --tags | sed 's/^colm-//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd colm
    autoreconf -fi
    ./configure --prefix=/usr
    make
}

package() {
    cd colm
    make DESTDIR="$pkgdir" install
    install -Dm0644 colm.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/colm.vim
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
