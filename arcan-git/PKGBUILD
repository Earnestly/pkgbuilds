pkgname=arcan-git
pkgver=0.5.5.pre1.r14.gf4ad0dc5
pkgrel=1

pkgdesc='multimedia framework'
url='https://arcan-fe.com/'
arch=('i686' 'x86_64')
license=('GPL' 'BSD')

depends=('ffmpeg' 'openal' 'sqlite3' 'luajit' 'apr' 'libusb' 'harfbuzz-icu'
         'libxkbcommon')
makedepends=('git' 'cmake' 'ruby')
optdepends=('arcan-utils: utilties such as acfgfs, aclip, and aloadimage')

provides=('arcan')
conflicts=('arcan')

source=('git+https://github.com/letoram/arcan')

sha256sums=('SKIP')

pkgver() {
    cd arcan
    git describe --long --tags | sed -r 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
    cd arcan/doc
    # Explicitly make ruby use UTF-8 encoding as when used in minimal
    # environments (where LANG is not set)
    ruby -Ku docgen.rb mangen

    cd "$srcdir"/arcan
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
          -DAPIMAN_DEST=/usr/share/man/arcan/man3 \
          -DVIDEO_PLATFORM=egl-dri src
    make VERBOSE=1
}

package() {
    cd arcan
    make DESTDIR="$pkgdir" install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
