pkgname=arcan-git
pkgver=0.6.0.pre1.r60.g58a40bad
pkgrel=1

pkgdesc='multimedia framework'
url='https://arcan-fe.com/'
arch=('i686' 'x86_64')
license=('GPL' 'BSD')

depends=('freetype2' 'openal' 'sqlite3' 'lua51' 'apr' 'sdl' 'libvncserver')

makedepends=('git' 'cmake' 'ruby' 'fuse3')
optdepends=('fuse3: to mount the window configuration filesystem with arcan_cfgfs')

provides=('arcan')
conflicts=('arcan')

source=('git+https://github.com/letoram/arcan')

sha256sums=('SKIP')

pkgver() {
    cd arcan
    git describe --long --tags | sed -r 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
    cd arcan

    # Explicitly make ruby use UTF-8 encoding explicitly when used in minimal
    # environments where LANG is not set.
    ruby -C doc -Ku docgen.rb mangen

    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DVIDEO_PLATFORM=sdl \
        -DLUA_INCLUDE_DIR=/usr/include/lua5.1 \
        -DDISABLE_HIJACK=OFF \
        -DDISABLE_JIT=ON src
    make VERBOSE=1
}

package() {
    cd arcan
    make DESTDIR="$pkgdir" install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
