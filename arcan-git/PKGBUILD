pkgname=arcan-git
pkgver=0.6.0.1.r85.gcaf9ffe3
pkgrel=1

pkgdesc='multimedia framework'
url='https://arcan-fe.com/'
arch=('x86_64')
license=('GPL' 'BSD')

depends=('openal' 'sqlite3' 'lua51' 'apr' 'sdl' 'libvncserver' 'ffmpeg'
         'libusb' 'libxkbcommon' 'xcb-util-wm' 'tesseract')

makedepends=('git' 'cmake' 'ruby' 'fuse3')
optdepends=('fuse3: to mount the window configuration filesystem with arcan_cfgfs')

provides=('arcan')
conflicts=('arcan')

source=('git+https://github.com/letoram/arcan')

sha256sums=('SKIP')

pkgver() {
    cd arcan
    git describe --long --tags | sed -r 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
    cd arcan

    # Explicitly make ruby use UTF-8 encoding when the LANG environment is
    # potentially unavailable.
    ruby -C doc -Ku docgen.rb mangen

    CFLAGS+=" -fmacro-prefix-map=$PWD/=" cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DVIDEO_PLATFORM=egl-dri \
        -DLUA_INCLUDE_DIR=/usr/include/lua5.1 \
        -DDISABLE_HIJACK=OFF \
        -DDISABLE_JIT=ON src
    make VERBOSE=1
}

package() {
    cd arcan
    make DESTDIR="$pkgdir" install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
