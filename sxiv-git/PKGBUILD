pkgname=sxiv-git
pkgver=26.r0.g1d28627
pkgrel=1

pkgdesc='x image viewer'
url='https://github.com/muennich/sxiv'
arch=('x86_64')
license=('GPL')

depends=('libexif' 'imlib2' 'libxft')
makedepends=('git')

provides=('sxiv')
conflicts=('sxiv')

source=('git+https://github.com/muennich/sxiv'
        'greater-zoom-levels.diff')

sha256sums=('SKIP'
            '5db25543cd6b3075bf99f3fb5d0faa2ed78ac7a5756318789aedb73c35a03deb')

prepare() {
    cd sxiv
    patch -Np1 -i "$srcdir"/greater-zoom-levels.diff
}

pkgver() {
    cd sxiv
    git describe --long --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd sxiv
    make
}

package() {
    cd sxiv
    make PREFIX=/usr DESTDIR="$pkgdir" install
}
