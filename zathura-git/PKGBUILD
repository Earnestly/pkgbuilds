pkgname=zathura-git
pkgver=0.3.6.r21.g945929c
pkgrel=1

pkgdesc='modular document viewer supporting'
url='https://pwmt.org/projects/zathura/'
arch=('i686' 'x86_64')
license=('zlib')

depends=('girara')
makedepends=('git' 'wayland-protocols')

optdepends=('zathura-pdf-poppler: PDF support'
            'zathura-pdf-mupdf: PDF support'
            'zathura-djvu: DJVU support'
            'zathura-ps: PostScript (PS) support'
            'zathura-cb: Comic Book (CB) support')

provides=('zathura') 
conflicts=('zathura') 

source=('git+https://git.pwmt.org/pwmt/zathura.git#branch=develop')

sha1sums=('SKIP')

pkgver() {
    cd zathura
    git describe --tags --long | sed 's/-/.r/; s/-/./'
}

build() {
    cd zathura
    make
}

package() {
    cd zathura
    make DESTDIR="$pkgdir" PREFIX=/usr install
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
