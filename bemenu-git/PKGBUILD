pkgname=bemenu-git
pkgver=r397.ge74224a
pkgrel=1

pkgdesc='dynamic menu library and client program inspired by dmenu'
url='https://github.com/Cloudef/bemenu'
arch=('x86_64')
license=('GPL' 'LGPL')

depends=('pango')
makedepends=('git' 'libxkbcommon' 'libxinerama' 'wayland')
optdepends=('wayland: wayland support'
            'libxkbcommon: wayland support'
            'libxinerama: x11 support'
            'ncurses: curses support')

provides=('bemenu')
conflicts=('bemenu')

source=('git+https://github.com/Cloudef/bemenu')

sha256sums=('SKIP')

prepare() {
    cd bemenu
    # curses: fix list indent with scrollbar
    # cairo/curses: dmenu like indent for lists
    # cairo: make scrollbar 1 character thick
    git revert --no-edit -n b4fc82a3aacfce3eca4dabb1c3097e0743c76195 \
                            bb7db3e2b73611e293afba44b2a04e08886416b5 \
                            1c487ba808c785a150e4cb8e343ffdff60451f92
}

pkgver() {
    cd bemenu
    printf 'r%s.g%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd bemenu
    make PREFIX=/usr
}

check() {
    cd bemenu
    # make test
}

package() {
    cd bemenu
    make PREFIX=/usr DESTDIR="$pkgdir" install
}
