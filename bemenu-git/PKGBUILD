pkgname=bemenu-git
pkgver=r286.gf464f0e
pkgrel=1

pkgdesc='dynamic menu library and client program inspired by dmenu'
url='https://github.com/Cloudef/bemenu'
arch=('x86_64')
license=('GPL' 'LGPL')

depends=('pango')
makedepends=('git' 'cmake' 'libxkbcommon' 'libxinerama' 'wayland')
optdepends=('wayland: wayland support'
            'libxkbcommon: wayland support'
            'libxinerama: x11 support'
            'ncurses: curses support')

provides=('bemenu')
conflicts=('bemenu')

source=('git+https://github.com/Cloudef/bemenu')

sha256sums=('SKIP')

pkgver() {
    cd bemenu
    printf 'r%s.g%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd bemenu
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib .
    make VERBOSE=1
}

check() {
    cd bemenu
    make test
}

package() {
    cd bemenu
    make DESTDIR="$pkgdir" install
}
