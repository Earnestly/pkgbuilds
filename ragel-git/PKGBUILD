pkgname=ragel-git
pkgver=7.0.0.10.r32.gdbc37f49
pkgrel=1

pkgdesc='finite state machine generator for c, c++, and asm'
url='http://www.colm.net/open-source/ragel'
arch=('i686' 'x86_64')
license=('GPL')

depends=('colm')

provides=('ragel')
conflicts=('ragel')

source=('git://git.colm.net/ragel.git')

sha1sums=('SKIP')

pkgver() {
    cd ragel
    git describe --long --tags | sed 's/^ragel-//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd ragel
    autoreconf -fi
    ./configure --prefix=/usr
    make
}

package() {
    cd ragel
    make DESTDIR="$pkgdir/" install
    install -Dm0644 ragel.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/ragel.vim

    install -Dm0644 contrib/ragel.m4 "$pkgdir"/usr/share/ragel/contrib/ragel.m4
    install -Dm0644 contrib/ragel.make "$pkgdir"/usr/share/ragel/contrib/ragel.make
    install -Dm0644 contrib/unicode2ragel.rb "$pkgdir"/usr/share/ragel/contrib/unicode2ragel.rb

    install -Dm0644 examples/*.rl -t "$pkgdir"/usr/share/ragel/examples
    install -Dm0644 examples/README examples/*.awk examples/cppscan* -t "$pkgdir"/usr/share/ragel/examples
}
