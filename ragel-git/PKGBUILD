pkgname=ragel-git
pkgver=7.0.0.11.r3442.gcc16042c
pkgrel=1

pkgdesc='finite state machine generator'
url='http://www.colm.net/open-source/ragel'
arch=('x86_64')
license=('GPL')

depends=('colm')

provides=('ragel')
conflicts=('ragel')

source=('git+https://github.com/adrian-thurston/ragel')

sha256sums=('SKIP')

pkgver() {
    cd ragel
    git describe --long --tags | sed 's/^ragel-//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd ragel
    # XXX Requires that automake --add-missing is run first.
    # autoreconf -fi

    ./autogen.sh
    ./configure --prefix=/usr
    make
}

package() {
    cd ragel
    make DESTDIR="$pkgdir/" install
    install -Dm0644 ragel.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/ragel.vim

    install -Dm0644 contrib/ragel.m4 "$pkgdir"/usr/share/ragel/contrib/ragel.m4
    install -Dm0644 contrib/ragel.make "$pkgdir"/usr/share/ragel/contrib/ragel.make
    install -Dm0644 contrib/unicode2ragel.rb "$pkgdir"/usr/share/ragel/contrib/unicode2ragel.rb

    install -Dm0644 examples/*.rl -t "$pkgdir"/usr/share/ragel/examples
    install -Dm0644 examples/README examples/*.awk examples/cppscan* -t "$pkgdir"/usr/share/ragel/examples
}
