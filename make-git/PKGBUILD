pkgname=make-git
pkgver=4.2.1.r116.g1c045bc
pkgrel=1

pkgdesc='GNU make utility to maintain groups of programs'
url='https://www.gnu.org/software/make/'
arch=('i686' 'x86_64')
license=('GPL')

options=('!makeflags')

depends=('guile')
makedepends=('git')

groups=('base-devel')

provides=('make')
conflicts=('make')

source=('git+https://git.savannah.gnu.org/git/make.git'
        'git://git.sv.gnu.org/gnulib')

sha256sums=('SKIP' 'SKIP')

pkgver() {
    cd make
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd make
    # XXX Requires internet connection to downloads .po files.
    ./bootstrap --gnulib-srcdir="$srcdir"/gnulib
    autoreconf -fi
    ./configure --prefix=/usr --with-guile
    make
}

check() {
    cd make
    make check
}

package() {
    cd make
    make DESTDIR="$pkgdir" install
}
