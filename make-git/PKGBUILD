pkgname=make-git
pkgver=4.3.r46.g9043b28
pkgrel=1

pkgdesc='GNU make utility to maintain groups of programs'
url='https://www.gnu.org/software/make/'
arch=('x86_64')
license=('GPL')

options=('!makeflags')

depends=('guile')
makedepends=('git')

groups=('base-devel')

provides=('make')
conflicts=('make')

source=('git+https://git.savannah.gnu.org/git/make.git'
        'git+https://git.savannah.gnu.org/git/gnulib.git')

sha256sums=('SKIP' 'SKIP')

pkgver() {
    cd make
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd make
    # XXX Requires internet connection to downloads .po files.
    ./bootstrap --gnulib-srcdir="$srcdir"/gnulib
    autoreconf -fi

    # XXX lib/alloca.h:49:18: error: ignoring return value of '__builtin_alloca' declared with attribute 'warn_unused_result' [-Werror=unused-result]
    #     cc1: error: function may return address of local variable [-Werror=return-local-addr]
    CFLAGS="$CFLAGS -Wno-unused-result -Wno-return-local-addr"

    ./configure --prefix=/usr --with-guile
    make
}

check() {
    cd make
    make check
}

package() {
    cd make
    make DESTDIR="$pkgdir" install
}
