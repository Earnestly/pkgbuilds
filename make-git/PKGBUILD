pkgname=make-git
pkgver=4.2.1.r69.g8a731d1
pkgrel=1

pkgdesc='GNU make utility to maintain groups of programs'
url='https://www.gnu.org/software/make/'
arch=('i686' 'x86_64')
license=('GPL')

options=('!makeflags')

depends=('guile')
makedepends=('git')

groups=('base-devel')

provides=('make')
conflicts=('make')

source=('git+https://git.savannah.gnu.org/git/make.git')

sha256sums=('SKIP')

pkgver() {
    cd make
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd make
    autoreconf -fi
    ./configure --prefix=/usr --with-guile
    make update
    make
}

check() {
    cd make
    make check
}

package() {
    cd make
    make DESTDIR="$pkgdir" install
}
