pkgname=cloc-git
pkgver=1.82.r18.g4463907
pkgrel=1

pkgdesc='count lines of code'
url='https://github.com/AlDanial/cloc'
arch=('any')
license=('GPL')

depends=('perl-algorithm-diff' 'perl-regexp-common' 'perl-digest-md5')
optdepends=('perl-parallel-forkmanager: parallel data processing')

provides=('cloc')
conflicts=('cloc')

# git+https://github.com/dluxhu/perl-parallel-forkmanager
source=('git+https://github.com/AlDanial/cloc'
        'git+https://github.com/AlDanial/cloc_submodule_test')

sha256sums=('SKIP' 'SKIP')

pkgver() {
    cd cloc
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd cloc

    # Can't link this.  Helps run more tests bit still fails on unrelated
    # issues. https://github.com/AlDanial/cloc/issues/275#issuecomment-509561297
    # mv -f "$srcdir"/cloc_submodule_test Unix/cloc_submodule_test

    # https://github.com/AlDanial/cloc/issues/275#issuecomment-509024247
    sed -i 's/^install-bin: test-perl$/install-bin:/' Unix/Makefile
}

check() {
    cd cloc/Unix
    # Skip test-perl for now as it fails attempting to load the
    # parallel-forkmanager module.  Additionally the test-code tests involved
    # with git are also broken, so just run the basic syntax tests.
    make test-pod
    ./t/00_C.t
}

package() {
    cd cloc/Unix
    make DESTDIR="$pkgdir" install
}
