pkgname=cloc-git
pkgver=1.76.r52.gc2cc7a3
pkgrel=1

pkgdesc='count lines of code'
url='https://github.com/AlDanial/cloc'
arch=('any')
license=('GPL')

depends=('perl-algorithm-diff' 'perl-regexp-common' 'perl-digest-md5')

provides=('cloc')
conflicts=('cloc')

source=('git+https://github.com/AlDanial/cloc')

sha256sums=('SKIP')

pkgver() {
    cd cloc
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd cloc/Unix
    # https://github.com/AlDanial/cloc/issues/275
    # https://github.com/dluxhu/perl-parallel-forkmanager
    sed -i '/my \$HAVE_Parallel_ForkManager/ {s/1/0/}' cloc
}

package() {
    cd cloc/Unix
    make DESTDIR="$pkgdir" install
}
