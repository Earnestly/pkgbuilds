pkgname=lgogdownloader-git
pkgver=3.3.r20.g9804215
pkgrel=1

pkgdesc='unofficial downloader for gog.com'
url='https://github.com/Sude-/lgogdownloader'
arch=('i686' 'x86_64')
license=('WTFPL')

depends=('boost-libs' 'jsoncpp' 'liboauth' 'rhash' 'tinyxml2' 'htmlcxx')
makedepends=('boost' 'help2man' 'git')

provides=('lgogdownloader')
conflicts=('lgogdownloader')

source=('git+https://github.com/Sude-/lgogdownloader.git')

sha1sums=('SKIP')

pkgver() {
    cd lgogdownloader
    git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd lgogdownloader
    cmake -DCMAKE_INSTALL_PREFIX=/usr
    make VERBOSE=1
}

package() {
    cd lgogdownloader
    make DESTDIR="$pkgdir" install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
