pkgname=runit-tools
pkgver=2.1.2
pkgrel=2

pkgdesc='unix init scheme with service supervision without the init'
url='http://smarden.org/runit/'
arch=('x86_64')
license=('BSD')

depends=('glibc')

conflicts=('runit')

source=("http://smarden.org/runit/runit-$pkgver.tar.gz")

sha256sums=('6fd0160cb0cf1207de4e66754b6d39750cff14bb0aa66ab49490992c0c47ba18')

prepare() {
    cd admin/runit-"$pkgver"
    # Use /var/service instead of /service.  This largely doesn't matter here
    # as all my uses of the sv command will be backed by the SVDIR environment.
    sed -i 's:/service/:/var&:' src/sv.c;

    # This is a thing.
    printf '%s %s %s\n' "${CC:-gcc}" "$CPPFLAGS" "$CFLAGS" > src/conf-cc
    printf '%s %s\n' "${CC:-gcc}" "$LDFLAGS" > src/conf-ld
}

build() {
    cd admin/runit-"$pkgver"/src
    # XXX Lots of implicit declarations for stuff which uses DEFAULT_SOURCE
    make
}

check() {
    cd admin/runit-"$pkgver"/src
    make check
}

package() {
    cd admin/runit-"$pkgver"
    install -Dm0755 src/sv "$pkgdir"/usr/bin/sv
    install -Dm0755 src/chpst "$pkgdir"/usr/bin/chpst
    install -Dm0755 src/runsv "$pkgdir"/usr/bin/runsv
    install -Dm0755 src/svlogd "$pkgdir"/usr/bin/svlogd
    install -Dm0755 src/utmpset "$pkgdir"/usr/bin/utmpset
    install -Dm0755 src/runsvdir "$pkgdir"/usr/bin/runsvdir
    install -Dm0755 src/runsvchdir "$pkgdir"/usr/bin/runsvchdir
    install -Dm0644 man/* -t "$pkgdir"/usr/share/man/man8
    install -Dm0644 package/COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
