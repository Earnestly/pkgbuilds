pkgname=sx-git
pkgver=r23.g1c4fbda
pkgrel=1

pkgdesc='start an xserver'
url='https://github.com/Earnestly/sx'
arch=('any')
license=('MIT')

depends=('xorg-server' 'xorg-xauth')
makedepends=('git')

provides=('sx')
conflicts=('sx')

source=('git+https://github.com/Earnestly/sx')

sha256sums=('SKIP')

pkgver() {
    cd sx
    printf 'r%s.g%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd sx
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
