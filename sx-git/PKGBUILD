pkgname=sx-git
pkgver=1.1.r3.g887df36
pkgrel=1

pkgdesc='start an xserver'
url='https://github.com/Earnestly/sx'
arch=('any')
license=('MIT')

depends=('xorg-server' 'xorg-xauth')
makedepends=('git')

provides=('sx')
conflicts=('sx')

source=('git+https://github.com/Earnestly/sx')

sha256sums=('SKIP')

pkgver() {
    cd sx
    git describe --tags --long | sed 's/-/.r/; s/-/./'
}

package() {
    cd sx
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
