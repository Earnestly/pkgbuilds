pkgname=qcachegrind-git
pkgver=19.04.0.r2.g46422e1
pkgrel=1

pkgdesc='visualization of performance profiling data'
url='http://kcachegrind.sourceforge.net'
arch=('i686' 'x86_64')
license=('GPL')

depends=('qt5-base')
makedepends=('git')
optdepends=('graphviz: to draw call graphs')

provides=('qcachegrind')
conflicts=('qcachegrind')

source=('git://anongit.kde.org/kcachegrind')

sha1sums=('SKIP')

pkgver() {
    cd kcachegrind
    git describe --long --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd kcachegrind
    qmake-qt5
    make
}

package() {
    cd kcachegrind
    install -Dm0755 cgview/cgview "$pkgdir"/usr/bin/cgview
    install -Dm0755 qcachegrind/qcachegrind "$pkgdir"/usr/bin/qcachegrind
    install -Dm0644 qcachegrind/qcachegrind.desktop "$pkgdir"/usr/share/applications/qcachegrind.desktop
}
