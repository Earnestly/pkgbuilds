pkgname=bcc-git
pkgver=0.11.0.r321.g104d2b3e
pkgrel=1

pkgdesc='bpf compiler collection'
url='https://github.com/iovisor/bcc'
arch=('i686' 'x86_64')
license=('Apache')

depends=('libelf')
makedepends=('git' 'llvm')
# checkdepends=('python')
optdepends=('python: required for many of the tools'
            'linux-headers: required to compile ebpf programs')

provides=('bcc')
conflicts=('bcc')

source=('git+https://github.com/iovisor/bcc')

sha256sums=('SKIP')

pkgver() {
    cd bcc
    git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd bcc
    CFLAGS+=" -fmacro-prefix-map=$PWD/=" cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib .
    make VERBOSE=1
}

check() {
    cd bcc
    # XXX Uses sudo(8)
    # make test
}

package() {
    cd bcc
    make DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE.txt "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
