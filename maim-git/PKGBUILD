pkgname=maim-git
pkgver=5.5.2.r7.g15ecc37
pkgrel=1

pkgdesc='take screenshots of the desktop, similar to scrot'
url='https://github.com/naelstrof/maim'
arch=( 'i686' 'x86_64' )
license=('GPL3')

depends=('imlib2' 'libxrandr' 'libxfixes' 'glm')
makedepends=('cmake' 'git')
optdepends=('slop: region selection support')

provides=('maim')
conflicts=('maim')

source=('git+https://github.com/naelstrof/maim.git')

sha1sums=('SKIP')

pkgver() {
    cd maim
    git describe --long --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd maim
    cmake -DCMAKE_INSTALL_PREFIX=/usr
    make VERBOSE=1
}

package() {
    cd maim
    make DESTDIR="$pkgdir" install
}
