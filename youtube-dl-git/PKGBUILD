pkgname=youtube-dl-git
pkgver=2021.03.03.r11.gfc2c6d532
pkgrel=1

pkgdesc='tool to download videos from different websites'
url='https://github.com/ytdl-org/youtube-dl'
arch=('any')
license=('custom')

options=('!emptydirs')

depends=('python')
makedepends=('git' 'pandoc')
optdepends=('ffmpeg: video post-processing')

provides=('youtube-dl')
conflicts=('youtube-dl')

source=('git+https://github.com/rg3/youtube-dl') #?signed')

# Philipp Hagemeister
# Sergey M. <dstftw@gmail.com>
# validpgpkeys=('7D33D762FD6C35130481347FDB4B54CBA4826A18'
#               'ED7F5BF46B3BBED81C87368E2C393E0F18A9236D')

sha256sums=('SKIP')

pkgver() {
    cd youtube-dl
    # printf r%s.g%s "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    git describe --long --tags | sed -r 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
    cd youtube-dl
    python setup.py build

    # Generate the rest of the files manually because youtube-dl seems unable
    # to use the correct directory locations consistently.  Skipping fish.
    make youtube-dl.1 bash-completion zsh-completion
}

package() {
    cd youtube-dl
    python setup.py install --skip-build --root="$pkgdir" --optimize=1

    install -Dm0644 youtube-dl.1 "$pkgdir"/usr/share/man/man1/youtube-dl.1
    install -Dm0644 youtube-dl.zsh "$pkgdir"/usr/share/zsh/site-functions/_youtube-dl
    install -Dm0644 youtube-dl.bash-completion "$pkgdir"/usr/share/bash-completion/completions/youtube-dl

    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
