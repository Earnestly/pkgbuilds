pkgname=pacutils-git
pkgver=0.6.1.r0.ga8302fd
pkgrel=1

pkgdesc='ALPM front-end tools'
url='https://github.com/andrewgregory/pacutils'
arch=('i686' 'x86_64')
license=('MIT')

depends=('pacman')
makedepends=('git')
checkdepends=('perl')

provides=('pacutils')
conflicts=('pacutils')

source=('git+https://github.com/andrewgregory/pacutils'
        'git+https://github.com/andrewgregory/mINI.c'
        'git+https://github.com/andrewgregory/tap.c'
        'git+https://github.com/andrewgregory/globdir.c.git')

sha1sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')

pkgver() {
    cd pacutils
    git describe --long --tags | sed 's/^v//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

prepare() {
    cd pacutils
    git submodule init
    git config submodule.ext/mini.c.url "$srcdir"/mINI.c
    git config submodule.ext/globdir.c.url "$srcdir"/globdir.c
    git config submodule.t/tap.c.url "$srcdir"/tap.c
    git submodule update t/tap.c ext/mini.c ext/globdir.c
}

build() {
    cd pacutils
    make SYSCONFDIR=/etc LOCALSTATEDIR=/var
}

check() {
    cd pacutils
    make check
}

package() {
    cd pacutils
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
