pkgname=pyalpm-git
pkgver=0.9.1.r9.ge25c8cf
pkgrel=1

pkgdesc='libalpm bindings for python 3'
url='https://git.archlinux.org/pyalpm.git'
arch=('x86_64')
license=('GPL')

depends=('python' 'pacman')
makedepends=('git')
checkdepends=('python-pytest')

provides=('pyalpm')
conflicts=('pyalpm')

source=('git+https://git.archlinux.org/pyalpm.git')

# XXX Commits aren't signed.
# Jelle van der Waa <jelle@vdwaa.nl>
# validpgpkeys=('E499C79F53C96A54E572FEE1C06086337C50773E')

sha256sums=('SKIP')

pkgver() {
    cd pyalpm
    git describe --long --tags | sed -r 's/([^-]*-g)/r\1/; s/-/./g'
}

build() {
    cd pyalpm
    make
}

check() {
    cd pyalpm
    make PYTEST_COVERAGE_OPTIONS= test
}

package() {
    cd pyalpm
    python setup.py install --root="$pkgdir" --optimize=1
}
