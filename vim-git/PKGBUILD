pkgname=vim-git
pkgver=8.2.2590.r0.g12bce9588
pkgrel=1

pkgdesc='text editor'
url='http://www.vim.org'
arch=('x86_64')
license=('custom:vim')

depends=('nawk' 'acl' 'gpm' 'libxt' 'hicolor-icon-theme')
makedepends=('git' 'lua' 'python' 'ruby' 'tcl')
optdepends=('lua' 'perl' 'python' 'ruby' 'tcl')

provides=('gvim' 'xxd' 'vim' 'vim-runtime')
conflicts=('gvim' 'xxd' 'vim' 'vim-runtime')

source=('git+https://github.com/vim/vim')

sha256sums=("SKIP")

pkgver() {
    cd vim
    git describe --long --tags | sed -r 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

prepare() {
    cd vim
    awk '/VIM LICENSE/,/at your option./' runtime/doc/uganda.txt > LICENSE
}

build() {
    cd vim
    ./configure --prefix=/usr \
      --with-x \
      --enable-cscope \
      --enable-gui=no \
      --enable-terminal \
      --enable-multibyte \
      --enable-tcpinterp=dynamic \
      --enable-luainterp=dynamic \
      --enable-rubyinterp=dynamic \
      --enable-perlinterp=dynamic \
      --enable-python3interp=dynamic
    make
}

check() {
    cd vim
    # Not happy running under env(1)
    # TERM=xterm make test
}

package() {
    cd vim
    make DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

    # Provided by vi in Arch Linux
    rm -f -- "$pkgdir"/usr/bin/ex "$pkgdir"/usr/bin/view
    find "$pkgdir"/usr/share/man \( -name ex.1 -o -name view.1 \) -delete
}
