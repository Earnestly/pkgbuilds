pkgname=weston-git
pkgver=2.0.0.r61.g863e66b0
pkgrel=1

pkgdesc='reference implementation of a wayland compositor'
url='http://wayland.freedesktop.org'
arch=('i686' 'x86_64')
license=('MIT')

depends=('libxkbcommon' 'libunwind' 'libxcursor' 'libva' 'libinput' 'libwebp'
         'pango' 'colord')
makedepends=('git' 'wayland-protocols')

provides=('weston')
conflicts=('weston')

source=('git+https://anongit.freedesktop.org/git/wayland/weston')

sha1sums=('SKIP')

pkgver() {
    cd weston
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd weston
    # XXX requires passing -fPIC to build with -flto
    CFLAGS=-fPIC ./autogen.sh --prefix=/usr \
        --libexecdir=/usr/lib/weston \
        --enable-demo-clients-install
    make
}

package() {
    cd weston
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
