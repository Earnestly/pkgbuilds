pkgname=weston-git
pkgver=6.0.0.r92.gd93c0f70
pkgrel=1

pkgdesc='reference implementation of a wayland compositor'
url='http://wayland.freedesktop.org'
arch=('x86_64')
license=('MIT')

depends=('libva' 'libinput' 'pango' 'colord' 'freerdp')
makedepends=('git' 'meson' 'wayland-protocols')

provides=('weston')
conflicts=('weston')

source=('git+https://anongit.freedesktop.org/git/wayland/weston')

sha256sums=('SKIP')

pkgver() {
    cd weston
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd weston
    meson --prefix=/usr \
        --libexecdir=/usr/lib/weston \
        -Dsimple-dmabuf-drm=intel build
    ninja -C build
}

package() {
    cd weston
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
