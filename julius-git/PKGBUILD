pkgname=julius-git
pkgver=r1671.gb1ab1cd0
pkgrel=1

pkgdesc='open source implementation of caesar 3'
url='https://github.com/bvschaik/julius'
arch=('i686' 'x86_64')
license=('AGPL')

depends=('sdl2_mixer')
makedepends=('git')

provides=('julius')
conflicts=('julius')

source=('git+https://github.com/bvschaik/julius')

sha256sums=('SKIP')

pkgver() {
    cd julius
    printf 'r%s.g%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd julius
    CFLAGS+=" -fmacro-prefix-map=$PWD/=" cmake -DCMAKE_INSTALL_PREFIX=/usr .
    make VERBOSE=1
}

check() {
    cd julius
    make test
}

package() {
    cd julius
    make DESTDIR="$pkgdir" install
}
