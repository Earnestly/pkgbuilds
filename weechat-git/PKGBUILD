pkgname=weechat-git
pkgver=2.3.r92.geb0828ec6
pkgrel=1

pkgdesc='terminal based irc client'
url='http://www.weechat.org/'
arch=('i686' 'x86_64')
license=('GPL3')

depends=('curl' 'gnutls' 'libgcrypt' 'hicolor-icon-theme')
makedepends=('git' 'asciidoc' 'source-highlight' 'aspell' 'cmake' 'lua' 'perl'
             'python' 'ruby' 'tcl' 'guile' 'lua' 'php-embed')
checkdepends=('cpputest')
optdepends=('aspell' 'lua' 'perl' 'python' 'ruby' 'tcl' 'guile' 'php-embed')

provides=('weechat')
conflicts=('weechat')

source=('git+https://github.com/weechat/weechat.git')

sha256sums=('SKIP')

pkgver() {
    cd weechat
    git describe --long --tags | sed 's/^v//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd weechat
    _python=$(python -c 'import sys; print("{}.{}m".format(*sys.version_info))')

    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_JAVASCRIPT=OFF \
        -DENABLE_PYTHON3=ON \
        -DPYTHON_LIBRARY=/usr/lib/libpython"$_python".so \
        -DENABLE_TESTS=ON
    make VERBOSE=1
}

check() {
    cd weechat

    # XXX These tests require a UTF-8 locale, using en_US as a proxy for C.
    # XXX Currently there's no way to disable testing of specific plugins which
    #     we can't support.  Because of this some tests will fail.
    LANG=en_US.UTF-8 ctest -V || true
}

package() {
    cd weechat
    make DESTDIR="$pkgdir" install
}
