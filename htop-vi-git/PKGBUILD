pkgname=htop-vi-git
pkgver=2.2.0.r20.g402e46b
pkgrel=1

pkgdesc='interactive process viewer patched with rudimentary vi style motions'
url='http://htop.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')

depends=('glibc' 'ncurses')
makedepends=('git')

provides=('htop')
conflicts=('htop')

source=('git+https://github.com/hishamhm/htop'
        'basic-vi-keys.patch')

sha256sums=('SKIP'
            '81ef4a77dcfde4134def0f17e6933192c723bbd4a4259b61b5ebc539bd489c04')

pkgver() {
    cd htop
    git describe --tags | sed 's/-/.r/; s/-/./'
}

prepare() {
    cd htop
    patch -Np1 -i "$srcdir"/basic-vi-keys.patch
}

build() {
    cd htop
    autoreconf -fi
    ./configure --prefix=/usr --enable-cgroup --enable-unicode
    make
}

package() {
    cd htop
    make DESTDIR="$pkgdir" install
}
